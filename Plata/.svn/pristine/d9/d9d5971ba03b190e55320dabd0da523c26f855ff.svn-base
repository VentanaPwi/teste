* CRIA TOOLBAR
_SCREEN.AddProperty('TOOLBAR')
_SCREEN.TOOLBAR = CREATEOBJECT('TOOLBAR')

* CRIA BOTOES E CAIXA DE TEXTO NA TOOLBAR
_SCREEN.TOOLBAR.ADDOBJECT('cmdMODIFORM','cmdMODIFORM')
_SCREEN.TOOLBAR.ADDOBJECT('cmdSAIR','cmdSAIR')
_SCREEN.TOOLBAR.ADDOBJECT('cmdADDUPG','cmdADDUPG')
_SCREEN.TOOLBAR.ADDOBJECT('txtNR_UPG','TEXTBOX')
_SCREEN.TOOLBAR.ADDOBJECT('cmdGERARUPG','cmdGERARUPG')

* DEFINE TEXTBOX
_SCREEN.TOOLBAR.txtNR_UPG.VALUE = 0
_SCREEN.TOOLBAR.txtNR_UPG.ENABLED = .F.
_SCREEN.TOOLBAR.txtNR_UPG.DISABLEDFORECOLOR = RGB(0,0,0)
_SCREEN.TOOLBAR.txtNR_UPG.WIDTH = 50

* CRIA PROPRIEDADE ADICIONAL
_SCREEN.TOOLBAR.ADDPROPERTY('cUPGFORM','')

* CONFIGURA TOOLBAR
_SCREEN.TOOLBAR.DOCK(0)
_SCREEN.TOOLBAR.VISIBLE = .T.
SET SYSMENU TO DEFA

* DEFINE NOVOS KEY LABELS
ON KEY LABEL F5 _SCREEN.TOOLBAR.cmdMODIFORM.CLICK()
ON KEY LABEL F6 _SCREEN.TOOLBAR.cmdADDUPG.CLICK()
ON KEY LABEL CTRL+X _SCREEN.TOOLBAR.cmdSAIR.CLICK()

* COMMAND MODIFICA FORM
DEFINE CLASS cmdMODIFORM AS CommandButton
*****************************************
	WIDTH = 70
	VISIBLE = .T.
	HEIGHT = 25
	CAPTION = 'Modi Form'
	
	PROC CLICK
	**********
		LOCAL lcMACRO,lcFORM AS String
		LOCAL llERRO AS Logical
		STORE .F. TO llERRO
		* TENTA PEGAR NOME DO FORM ATIVO
		TRY 
			lcFORM = ALLTRIM(SYS(1271,_SCREEN.ActiveForm))
		CATCH
			llERRO = .T.
		ENDTRY
		* CASO NAO SEJA UM FORM VÁLIDO
		IF llERRO = .T.
			RETURN
		ENDIF
		* VERIFICA SE REALMENTE É UM FORM
		IF VARTYPE(_SCREEN.ActiveForm) <> 'O'
			RETURN
		ENDIF
		IF VARTYPE(lcFORM) <> 'C'
			RETURN
		ENDIF
		IF NOT FILE(lcFORM)
			RETURN
		ENDIF
		_SCREEN.ActiveForm.RELEASE()
		* SUSPENDE PROGRAMA E ABRE MODO EDICAO
		KEYBOARD "{CTRL+F2}" + "{DNARROW}"
		lcMACRO = [KEYBOARD "SUSP" + "{SHIFT+HOME}" + "{ENTER}" + "MODIFY FORM ];
			+ lcFORM + [" + "{ENTER}"]
		EXECSCRIPT(lcMACRO)
	ENDPROC
ENDDEFINE

* COMMAND SAIR
DEFINE CLASS cmdSAIR AS CommandButton
*****************************************
	WIDTH = 50
	VISIBLE = .T.
	HEIGHT = 25
	CAPTION = 'Sair'
	
	PROC CLICK
	**********
		ON KEY LABEL F5
		ON KEY LABEL F6
		ON KEY LABEL CTRL+X
		KEYBOARD "{CTRL+F2}"
		DO ENCERRAR
	ENDPROC
ENDDEFINE

* COMMAND ADICIONA FORM PARA GERAR UPGRADE
DEFINE CLASS cmdADDUPG AS CommandButton
*****************************************
	WIDTH = 70
	VISIBLE = .T.
	HEIGHT = 25
	CAPTION = 'Upgrade'
	
	PROC CLICK
	**********
		LOCAL llERRO AS Logical
		STORE .F. TO llERRO
		
		* CASO SEJA UPGRADE MUDA BOTAO PARA ADD FORM
		IF UPPER(THIS.Caption) = 'UPGRADE'
			THIS.Caption = 'Add Form'
			_SCREEN.TOOLBAR.txtNR_UPG.VISIBLE = .T.
			_SCREEN.TOOLBAR.cmdGERARUPG.VISIBLE = .T.
			RETURN
		ENDIF
		* TENTA PEGAR O NOME DO FORM
		TRY 
			lcFORM = SYS(1271,_SCREEN.ActiveForm)
		CATCH
			llERRO = .T.	
		ENDTRY
		* VERIFICA SE DEU ERRO
		IF llERRO = .T.
			DO MENS WITH 'NAO FOI POSSIVEL ADICIONAR FORM ATIVO PARA O UPGRADE'
			RETURN
		ENDIF
		* VERIFICA SE REALMENTE É UM FORM
		IF VARTYPE(_SCREEN.ActiveForm) <> 'O'
			RETURN
		ENDIF
		IF VARTYPE(lcFORM) <> 'C'
			RETURN
		ENDIF
		IF NOT FILE(lcFORM)
			RETURN
		ENDIF
		_SCREEN.TOOLBAR.txtNR_UPG.VALUE = _SCREEN.TOOLBAR.txtNR_UPG.VALUE + 1
		_SCREEN.TOOLBAR.cUPGFORM = _SCREEN.TOOLBAR.cUPGFORM + lcFORM + '|'
		_SCREEN.ActiveForm.RELEASE()
	ENDPROC
ENDDEFINE

* COMMAND GERA UPGRADE
DEFINE CLASS cmdGERARUPG AS CommandButton
*****************************************
	WIDTH = 70
	HEIGHT = 25
	CAPTION = 'Gerar UPG'
	
	PROC CLICK
		GERAR_UPGRADER()
		_SCREEN.TOOLBAR.cmdGERARUPG.Visible = .F.
		_SCREEN.TOOLBAR.txtNR_UPG.Visible = .F.
		_SCREEN.TOOLBAR.txtNR_UPG.Value = 0
		_SCREEN.TOOLBAR.cmdADDUPG.Caption = 'Upgrade'
		_SCREEN.TOOLBAR.cUPGFORM = ''
	ENDPROC
ENDDEFINE

PROC GERAR_UPGRADER
********************
	LOCAL loZIP AS Object

	LOCAL lcUPG, lcZIP, lcCOMANDO, lcFORMS AS String
	STORE '' TO lcUPG, lcZIP, lcCOMANDO ,lcFORMS

	lcUPG = ALLTRIM(INPUTBOX('ESCOLHA O NOME PARA O ARQUIVO UPG'))
	IF EMPTY(lcUPG)
		DO MENS WITH 'UPGRADE CANCELADO!'
		RETURN
	ENDIF
	lcUPG	= FULLPATH('') + 'UPGRADE\' + ALLTRIM(UPPER(lcUPG)) + '.UPG'

	IF FILE(lcUPG)
		DELETE FILE &lcUPG
		IF FILE(lcUPG)
			DO MENS WITH 'FALHA: ARQUIVO DE UPGRADE NÃO PODE SER APAGADO: '+lcUPG
			RETURN
		ENDIF
	ENDIF

	IF SIMOUNAO('GERAR UPGRADE DOS FORMS SELECIONADOS?','CONFIRMACAO PARA GERAR UPGRADE',1)
		lcZIP = FULLPATH('') + 'TOOLS\ZIP.EXE'

		DO ESPERANDO WITH 'COMPACTANDO ARQUIVOS...'

		loZIP = CreateObject( "WScript.Shell" )

		lcFORMS = '|' + UPPER(_SCREEN.TOOLBAR.cUPGFORM)
		FOR lnI = 1 TO OCCURS('|',lcFORMS)
		lcARQUIVO = STREXTRACT(lcFORMS,'|','|',lnI)
			FOR lnI2 = 1 TO 2
				IF lnI2 = 2
					lcARQUIVO = STRTRAN(lcARQUIVO,'.SCX','.SCT')
				ENDIF
				IF FILE(lcARQUIVO)
					lcCOMANDO = lcZIP + " -o -j " + lcUPG + ' ' + lcARQUIVO
					loZIP.Run(lcCOMANDO, 0,.T.)
				ENDIF
			ENDFOR
		ENDFOR
		WAIT CLEAR
		DO MENS WITH 'ARQUIVO GERADO COM SUCESSO!' + CHR(13) + lcARQUIVO 
	ENDIF
ENDPROC