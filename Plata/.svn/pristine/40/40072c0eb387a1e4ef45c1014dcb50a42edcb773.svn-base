LOCAL lcCOMANDO, lcPADRAO AS String

TEXT TO lcCOMANDO NOSHOW PRETEXT 7
	SELECT 
		TAB.NAME AS TABELA,
		COL.NAME AS COLUNA,
		COL.XTYPE,
		COL.LENGTH
	FROM 
		SYSCOLUMNS COL
		INNER JOIN SYSOBJECTS TAB ON TAB.ID=COL.ID
	WHERE
		COLLATION = 'SQL_Latin1_General_CP1_CI_AS'
	ORDER BY
		TAB.NAME,
		COL.NAME
ENDTEXT
=PESQUISASQL(lcCOMANDO,'TMPTABS')

TEXT TO lcPADRAO NOSHOW PRETEXT 7
	ALTER TABLE
		[[TABELA]]
	ALTER COLUMN
		[[COLUNA]]
		[[TIPO]] ( [[TAMANHO]] )
		COLLATE SQL_Latin1_General_CP1_CI_AI
ENDTEXT

SELE TMPTABS
GO TOP
SCAN WHILE NOT EOF()
	IF TMPTABS.XTYPE=35
		* CAMPO TIPO TEXT NÃO PODE SER ALTERADO
		LOOP
	ENDIF
	IF 'PK_ID' $ ALLTRIM(UPPER(TMPTABS.COLUNA))
		* CAMPOS PK_ID TEM INDICE E NÃO PODE SER ALTERADO
		LOOP
	ENDIF
	lcCOMANDO	= lcPADRAO
	lcCOMANDO	= STRTRAN(lcCOMANDO,'[[TABELA]]',ALLTRIM(TMPTABS.TABELA))
	lcCOMANDO	= STRTRAN(lcCOMANDO,'[[COLUNA]]',ALLTRIM(TMPTABS.COLUNA))
	lcCOMANDO	= STRTRAN(lcCOMANDO,'[[TAMANHO]]',ALLTRIM(STR(TMPTABS.LENGTH)))
	IF TMPTABS.XTYPE = 167
		* CAMPO VARCHAR
		lcCOMANDO	= STRTRAN(lcCOMANDO,'[[TIPO]]','VARCHAR')
	ENDIF
	IF TMPTABS.XTYPE = 175
		* CAMPO CHAR
		lcCOMANDO	= STRTRAN(lcCOMANDO,'[[TIPO]]','CHAR')
	ENDIF
	=EXECUTASQL(lcCOMANDO)
ENDSCAN
